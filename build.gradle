buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.9"
}

apply plugin: "net.minecraftforge.gradle.forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Public config file.
ext.config = parseConfig(file('build.properties'))
// Private config file. Not uploaded to github.
ext.privateconfig = parseConfig(file('private.properties'))

version = "${config.mc_version}-${config.mod_version}"
group = config.group
archivesBaseName = config.mod_name

curseforge {
    apiKey = privateconfig.curse_api_key
    project {
        id = config.project_id
        releaseType = config.release_type.toLowerCase()
        changelog = config.changelog
        addGameVersion config.mc_version
    }
}

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "run"
    mappings = config.mc_mappings
    replace 'GRADLE:VERSION', config.mod_version
    replace 'GRADLE:MC_VERSION', config.mc_version
    replace 'GRADLE:NAME', config.mod_name
    replace 'GRADLE:MODID', config.mod_id
    makeObfSourceJar = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}
